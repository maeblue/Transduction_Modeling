#This file is designed to simulate the InAs interfaces

go atlas simflags="-P 1"

##########################################
### Section 1: Structure Specification ###
##########################################

# Here we define a 1 micron sensor region (big enough for "molecule" modeling)
mesh  space.mult=1

#Denser mesh near contacts
x.mesh	loc=0		spac=0.02
x.mesh	loc=1		spac=0.02

#Denser Mesh near the surface (y=0) through the thin film (~100nm)
y.mesh	loc=-0.20	spac=0.05
y.mesh	loc=0.00	spac=0.001
y.mesh	loc=0.10	spac=0.001
y.mesh	loc=0.15	spac=0.005
y.mesh	loc=0.25	spac=0.05
y.mesh	loc=0.30	spac=0.10
y.mesh	loc=0.50	spac=0.50

##################################
### Section 2: Material Models ###
##################################

#We have air on top of InAs on top of GaAs
region	num=1	material=air	x.min=0	x.max=1	y.min=-2.0	y.max=0.00
region	num=2	material=InAs	x.min=0	x.max=1	y.min=0.00	y.max=0.10
region	num=3	material=GaAs	x.min=0	x.max=1	y.min=0.10	y.max=0.50

#Anode on the left side, cathode on the right
electr  name=anode		x.min=0.0	x.max=0.1	y.min=-0.01	y.max=0.00
electr  name=cathode	x.min=0.9	x.max=1.0	y.min=-0.01	y.max=0.00

#Gaussian doping to simulate accumulation region near InAs surface
# (dir=y replaces the x.left and x.right parameters)
doping gaus peak=0.0050 char=0.0000001 conc=2e18 n.type dir=y

#Save the basic design
save outf=InAs_interfaces_structure.str
# tonyplot InAs_interfaces_structure.str

###########################
### Section 3: Modeling ###
###########################

# And add a work function to the Anode & Cathode, for Ti=4.33 Au=5.2
# could add "CURRENT" keyword, if we want current-controlled stuff
contact	name=anode	workf=4.76
contact	name=cathode	workf=4.76

# Add mobilities for InAs and GaAs regions (according to our data)
# Don't have good data yet for our InAs
MATERIAL	MATERIAL=InAs	MUN=1e5

# InAs has an electron affinity of 4.9
# Air apparently has a bandgap of 9
# MATERIAL NAME=Air	AFFINITY=5.1
# MATERIAL NAME=GaAs	AFFINITY=4.5

# We could specify fixed charge and surface recomb. vel. here
# INTERFACE QF=3e10 X.MIN=1.0 X.MAX=2 Y.MIN=0.0 Y.MAX=0.5

# Analytic for mobility, fldmob for velocity saturation
# and WATT for transverse field model
# model  conmob ANALYTIC  fldmob WATT  srh  auger  bgn
# That didn't run, so I took out some things
# Include Klaassen Band-to-Band transitions (direct & indirect)
model  BOLTZMANN conmob  fldmob  srh  auger  bgn BBT.KL

#############################################
### Section 3: Numerical Method Selection ###
#############################################

#Start with the gummel method to get good initial guess, then switch to
#have to use newton or block for current controlled stuff
method gummel newton

#And we tell it what values to solve each model for within the mesh
output con.band val.band u.srh u.aug flowlines qfn qfp

#Log the tests
log outf=InAs_interfaces.log

#And then we solve for the initial conditions
solve  init

#Now we re-save our structure file with all of these solutions in it
save outf=InAs_interfaces.str
tonyplot InAs_interfaces.str -set band_structure.set


#We run a 400uA current throught he electrodes and see what happens
solve	VANODE=0 VSTEP=1 IFINAL=5 NAME=anode outf=InAs_interfaces-vsweep.str master

tonyplot InAs_interfaces-vsweep.str
tonyplot InAs_interfaces.log

quit
